package flat/website/functions

import flat/io/File
import flat/io/FileReader
import flat/json/JsonDeserializer
import flat/datastruct/HashMap
import flat/http/Http
import flat/time/Timer

class {
  let static String pageHtml = FileReader(File("res/github-status.html")).readAllContents()

  public construct() {}

  public async run() -> String {
    let Array<HashMap<String, String>> badges = JsonDeserializer().parseArray(File("res/badges.json"))
    let timer = Timer():start()

    let svgs = badges.mapParallel((badge) => {
      let svgData = Http.get(badge["svg"]).data

      return HashMap()
        :add("svg", svgData)
        :add("url", badge["url"])
        :add("label", badge["label"])
    })

    Console.writeLine("GitHubStatusPage: #{timer.stop()}")

    let svgContents = svgs.map((entry, i) => {
      let aTagStart = "<" + "a target=\"_blank\" href=\"" + entry["url"] + "\"" + ">";
      let aTagEnd = "<" + "/a" + ">"
      let svgContent = entry["svg"]

      return aTagStart +
        svgContent +
        aTagEnd
    })

    return pageHtml.replace("{{badges}}", svgContents.join(""))
  }
}
